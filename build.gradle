
plugins {
    id 'java'
}

group = 'com.smartkids'
version = '1.0-SNAPSHOT'

// server settings
def remoteServerLoc = "$buildDir/../../Server"
def serverFile = "paper-1.19.2-237.jar"

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }
    maven {
        name = 'enginehub'
        url = 'https://maven.enginehub.org/repo/'
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.2.12'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.7'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.register("removeOld", Delete) {
  delete remoteServerLoc + "/plugins/" + rootProject.name+"-"+ version+".jar"
}

tasks.register("copyBuild",Copy) {
  dependsOn("build")
  dependsOn("removeOld")
  from "$buildDir/libs/"+rootProject.name+"-"+version+".jar"
  into remoteServerLoc+"/plugins"
} 

tasks.register("UpdateServer") {
  dependsOn("copyBuild")
  //todo - be more specific
  println("")
  println(new String((char) 27) + "[31m" + "---Please run 'plugman load CodewizClassRegions' on server!---" + new String((char) 27) + "[0m");
}


tasks.register("RunServer",Exec) {
  dependsOn("copyBuild")
  workingDir remoteServerLoc
  commandLine "java", "-jar", serverFile
}
